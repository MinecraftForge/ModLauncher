pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            plugin('apt', 'com.diffplug.eclipse.apt').version('4.3.0')
            plugin('license', 'net.minecraftforge.licenser').version('1.2.0')
            plugin('gradleutils', 'net.minecraftforge.gradleutils').version('[2.3,2.4)')
            plugin('versions', 'com.github.ben-manes.versions').version('0.52.0')
            plugin('modules', 'org.gradlex.extra-java-module-info').version('1.5')
            
            version('asm', '9.7.1')
            library('asm',         'org.ow2.asm', 'asm'        ).versionRef('asm')
            library('asm-tree',    'org.ow2.asm', 'asm-tree'   ).versionRef('asm')
            library('asm-commons', 'org.ow2.asm', 'asm-commons').versionRef('asm')
            bundle('asm', ['asm', 'asm-tree', 'asm-commons'])

            version('junit', '5.10.2')
            library('junit-api', 'org.junit.jupiter', 'junit-jupiter-api').versionRef('junit')
            library('junit-engine', 'org.junit.jupiter', 'junit-jupiter-engine').versionRef('junit')
            library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.10.2')
            bundle('junit-runtime', ['junit-engine', 'junit-platform-launcher']) // Force Gradle to load the JUnit Platform Launcher from the module-path

            library('nulls', 'org.jetbrains:annotations:24.1.0')
            library('unsafe', 'net.minecraftforge:unsafe:0.9.2')
            library('securemodules', 'net.minecraftforge:securemodules:2.2.14')
            library('jopt-simple', 'net.sf.jopt-simple:jopt-simple:5.0.4')
            library('bootstrap-api', 'net.minecraftforge:bootstrap-api:2.0.0')

            version('log4j', '2.22.1')
            library('log4j-api',  'org.apache.logging.log4j', 'log4j-api' ).versionRef('log4j')
            library('log4j-core', 'org.apache.logging.log4j', 'log4j-core').versionRef('log4j')
            bundle('log4j-runtime', ['log4j-api', 'log4j-core'])

            version('jmh', '1.37')
            library('jmh-core', 'org.openjdk.jmh', 'jmh-core').versionRef('jmh')
            library('jmh-ap', 'org.openjdk.jmh', 'jmh-generator-annprocess').versionRef('jmh')
        }
    }
}

rootProject.name = 'modlauncher'

enableFeaturePreview 'TYPESAFE_PROJECT_ACCESSORS'

include 'ml-jmh'
include 'ml-test'
include 'ml-test-jar'
include 'modlauncher-harness'